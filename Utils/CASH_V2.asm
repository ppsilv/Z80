
	ORG	#8000
;FONT "153"
	DI 
	XOR	A
	OUT	(#FE),A
	LD	HL,#4000
	LD	DE,#4001
	LD	BC,#1AFF
	LD	(HL),A
	LDIR 
	CALL	PRI64
	DEFB	#16,0,0,#FF,5
	DEFB	"Cash-RAM verify utility ver2.0	 "
	DEFB	"(C) Written by	(R)soft	1996-98",#0D
	DEFB	"Support 2,4,8,16Kb CASH MEMORY!",#0D
	DEFB	#FF,3,"Select Cash-RAM by IN A,(#FB) & "
	DEFB	"ROM by	IN A,(#7B).",#0D,#0D
	DEFB	#FF,5,"Warning!	This program destroyed "
	DEFB	"information in	Cash-RAM.",13
	DEFB	"Save info to disk and continue.",#0D
	DEFB	#FF,6,"Save CASH? (Y/N)	",0
CILK
	LD	A,#DF
	IN	A,(#FE)
	BIT	4,A
	JR	Z,SAVEINF
	LD	A,#7F
	IN	A,(#FE)
	BIT	3,A
	JR	Z,NOSAVE
	JR	CILK
SAVEINF
	IN	A,(#FB)
	LD	HL,0
	LD	DE,#C000
	LD	BC,#4000
	LDIR 
	IN	A,(#7B)
	CALL	PRI64
	DEFB	"Yes.",#0D,"Save info.",#0D,0
	LD	HL,NAME
	LD	DE,23773
	LD	BC,9
	LDIR 
	LD	C,#0B
	LD	HL,#C000
	LD	DE,#4000
	CALL	#3D13
	DI 
	JR	CONTIO
NOSAVE
	CALL	PRI64
	DEFB	"No.",#0D,0
CONTIO
;-----------------------
	CALL	PRI64
	DEFB	#0D,#FF,7,"Cash-RAM: ",0
	CALL	TST_CASH
	LD	A,H
	AND	A
	JP	Z,NOCASH
;----- HL = äéã-Çé íÖëí. ÅÄâí -------
	LD	(LENCASH),HL
	CALL	PRI64
	DEFB	#FF,7
	DEFB	"Detected ",0
	LD	A,(LENCASH+1)
	LD	HL,TXT2
	CP	7
	JR	Z,PRIKIL
	LD	HL,TXT4
	CP	#0F
	JR	Z,PRIKIL
	LD	HL,TXT8
	CP	#1F
	JR	Z,PRIKIL
	LD	HL,TXT16
PRIKIL
	LD	DE,FOKIL
	LDI 
	LDI 
	CALL	PRI64
FOKIL	DEFB	"  Kb memory. Wait!",#0D
	DEFB	#FF,3,"Pass:",0
	XOR	A
CICL
	PUSH	AF
	PUSH	AF
	PUSH	AF
	CALL	PRI64
	DEFB	#16,9,5,0
	POP	AF
	CALL	PRIHB
	POP	AF
	ADD	A,A
	LD	HL,TABLES
	LD	D,0
	LD	E,A
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	CALL	TEST
	JP	C,ERRORC
	POP	AF
	CP	#1F
	JR	Z,OK
	INC	A
	JR	CICL
OK
	CALL	PRI64
	DEFB	#0D,#FF,7,"OK.",#0D,0
ENDTEST
	CALL	PRI64
	DEFB	#0D,"End test.",#0D,0
	IN	A,(#7B)
	EI 
	RST	8
	DEFB	#FF
	RET 
TST_CASH
	IN	A,(#FB)		;!!!
	LD	HL,0
	LD	DE,1
	LD	BC,#3FFF
	LD	(HL),L
	LDIR 
	LD	HL,#00FF
	LD	B,#40
ML1
	LD	(HL),H
	INC	H
	DJNZ	ML1
	LD	H,0	;!!! 0
	LD	A,(55)
	AND	A
	RET	NZ	;HL=0 -	No Cash
	LD	E,#3F
	LD	HL,#07FF
	LD	A,(HL)
	CP	E
	RET	Z      ;HL=07FF
	LD	H,#0F
	LD	A,(HL)
	CP	E
	RET	Z	;HL=0FFF
	LD	H,#1F
	LD	A,(HL)
	CP	E
	RET	Z      ;HL=1FFF
	LD	H,#3F
	RET		;HL=3FFF
TXT2	DEFB	" 2"
TXT4	DEFB	" 4"
TXT8	DEFB	" 8"
TXT16	DEFB	"16"
NAME	DEFB	"CASHINFOC"
ERRORC
	LD	B,(HL)
	LD	(B1ERR),A
	LD	A,B
	LD	(B2ERR),A
	LD	(FOER),HL
	IN	A,(#7B)
	POP	AF
	CALL	PRI64
	DEFB	#0D,"Error at Address:#",0
	LD	HL,0
FOER	EQU	$-2
	CALL	PRIHEX
	CALL	PRI64
	DEFB	" Have:#",0
	LD	A,0
B2ERR	EQU	$-1
	CALL	PRIHB
	CALL	PRI64
	DEFB	"  --  Have been:#",0
	LD	A,0
B1ERR	EQU	$-1
	CALL	PRIHB
	CALL	PRI64
	DEFB	".",#0D,0
	JP	ENDTEST
NOCASH
	CALL	PRI64
	DEFB	#FF,6
	DEFB	"Not detected.",#0D,0
	JP	ENDTEST
TABLES	DEFW	#1111,#3333,#7777,#FFFF
	DEFW	#5555,#AAAA,#55AA,#AA55
	DEFW	#0F0F,#F0F0,#FF00,#00FF
	DEFW	#1234,#5678,#9ABC,#DEF0
	DEFW	#3838,#2677,#A5A5,#5A5A
	DEFW	#AFAF,#F5F5,#FF55,#AAFF
	DEFW	#5F5F,#FA50,#0FA5,#FAAF
	DEFW	#EEEE,#CCCC,#8888,#0000
TEST
	LD	BC,0
LENCASH	EQU	$-2
	DEC	BC
	LD	A,H
	LD	DE,0
	LD	(DE),A
	LD	(TST1),A
	INC	DE
	LD	A,L
	LD	(DE),A
	LD	(TST2),A
	INC	DE
	LD	HL,0
	PUSH	HL
	PUSH	BC
	LDIR 
	POP	BC
	POP	HL
TEST2
	LD	A,0
TST1	EQU	$-1
	CPI 
	JR	NZ,ERRO
	LD	A,0
TST2	EQU	$-1
	CPI 
	JR	NZ,ERRO
	LD	A,C
	OR	B
	JP	NZ,TEST2
	AND	A
	RET 
ERRO
	DEC	HL
	SCF 
	RET 
;----------------------
PRIHEX	LD	A,H
	PUSH	HL
	CALL	PRIHB
	POP	HL
	LD	A,L
PRIHB	PUSH	AF
	RRCA 
	RRCA 
	RRCA 
	RRCA 
	CALL	PRITET
	POP	AF
PRITET	AND	#0F
	ADD	A,#90
	DAA 
	ADC	A,#40
	DAA 
	JP	PRIA
PRI64	POP	HL
	CALL	LL73EF
	JP	(HL)
LL73EF	LD	A,(HL)
	INC	HL
	OR	A
	RET	Z
	CP	#16
	JR	Z,LL7409
	CP	#FF
	JR	Z,LL7402
	EXX 
	CALL	LL7425
	EXX 
	JR	LL73EF
LL7402	LD	A,(HL)
	INC	HL
	LD	(LL7440),A
	JR	LL73EF
LL7409	LD	D,(HL)
	INC	HL
	LD	E,(HL)
	INC	HL
	PUSH	HL
	LD	(LL6012),DE
	CALL	SODA2
	POP	HL
	JR	LL73EF
LLSPACE	LD	A," "
LL7425	CP	#0D
	JR	Z,LL746A
PRIA
	LD	(LL7454),A
	LD	A,(LL6012)
	LD	B,A
	SRL	A
	LD	C,A
	LD	HL,0
LL743A	EQU	$-2
	LD	A,L
	ADD	A,C
	LD	L,A
	LD	(HL),7
LL7440	EQU	$-1
	LD	DE,0
LL7442	EQU	$-2
	LD	A,E
	ADD	A,C
	LD	E,A
	LD	A,B
	RRA 
	SBC	A,A
	XOR	#F0
	LD	(LL745A),A
	CPL 
	LD	(LL745E),A
	LD	HL,FONT
LL7454	EQU	$-2
	LD	B,8
LL7458	LD	A,(HL)
	AND	0
LL745A	EQU	$-1
	LD	C,A
	LD	A,(DE)
	AND	0
LL745E	EQU	$-1
	OR	C
	LD	(DE),A
	INC	H
	INC	D
	DJNZ	LL7458
	LD	HL,LL6012
	INC	(HL)
	LD	A,(HL)
	CP	64
	JR	NC,LL746A
	RET 
LL746A
	LD	HL,LL6012
	LD	(HL),0
	INC	HL
	LD	A,(HL)
	INC	A
	CP	24
	JR	NC,SDVIG
	LD	(HL),A
	JR	SD2
SDVIG
	LD	A,23
	LD	(HL),A
	LD	B,0
	CALL	#DFE
SD2
	LD	DE,(LL6012)
SODA2
	LD	E,0
	CALL	LL6D3D	;ADR SCR
	LD	(LL7442),HL
	CALL	LL6D4C	;ADR ATR
	LD	(LL743A),HL
	RET 
LL6D3D	LD	A,D
	AND	#18
	ADD	A,#40
	LD	H,A
	LD	A,D
	AND	7
	RRCA 
	RRCA 
	RRCA 
	ADD	A,E
	LD	L,A
	RET 
LL6D4C	LD	H,0
	LD	L,D
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	A,L
	ADD	A,E
	LD	L,A
	LD	A,#58
	ADD	A,H
	LD	H,A
	RET 
LL6012	DEFW	0
EOF
	ORG	#8500
FONT
	INCBIN	"153"
	ORG	#8000







